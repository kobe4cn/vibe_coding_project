# GML 语言规格说明

## 1. 快速入门

GML（Generic Mapping Language）是一种声明式数据映射语言，用于将源数据结构转换为目标数据结构。

### 1.1 对象构造

当目标结果是一个包含多个字段的对象时，使用对象构造模式。语法为一组 `字段 = 表达式` 的赋值语句：

```gml
id   = user.id
city = user.addr.city
vip  = true
```

支持单行形式，以简化简单映射的编写（逗号分隔）：

```gml
id = user.id, city = user.addr.city, vip = true
```

此模式始终生成一个新对象，字段的值由 `=` 右侧的表达式求值获取。

此外，GML 提供紧凑对象语法（类似 JavaScript 的属性简写，但省略了`{}`包装）：

```gml
id, name, age = 20
```

该写法等价于：

```gml
id = id, name = name, age = 20
```

### 1.2 单值映射

当希望目标结果直接返回某个值（如字符串、数字、布尔值、或整个源对象），可使用单值映射模式，此时 GML 中仅需包含一个表达式：

```gml
'success'        # 返回字符串字面量
user             # 返回 user 对象本身
user.addr.city   # 返回嵌套字段 city 的值
null             # 返回空值
```

此模式的输出类型完全由表达式决定，GML 不对其进行结构包装。

### 1.3 结果返回

1. **单变量脚本**  
- 脚本只包含单一变量标识符（如 `id`），GML 默认按`单值映射`处理。  
- 如需构造为对象，需显式包装为 `{ id }` 方式。  
> 注：此规则仅适用于脚本仅含单一变量标识符的情形，`user.id` 或 `a + b` 不在此情形中。


2. **多语句脚本**  
脚本的整体模式由最后一项表达式决定：
- `a = 1, b = 2, this.a + this.b` 被视为`单值映射`，前序映射项作为临时变量处理；
- `a = 1, b = 2, c` 被视为`对象构造`，因该脚本并非只包含单一变量标识符。


3. **显式变量声明**  
- GML使用`$`作为临时变量前缀，如：`$a = 1, $b = 2, c = this.$a + this.$b`，执行结果为：`{c: 3}`。  
- 所有以 `$` 开头的变量不会出现在输出中，可通过 `this.$var` 引用已声明的临时变量。  
- 通常用于`对象构造`，避免将临时变量作为对象的字段返回，`单值映射`可以不使用该模式。


4. **显式返回结果**  
- 使用 `return` 关键字显式指定返回值，例如：`return order`  
- `return` 语句只能出现在脚本末尾

### 1.4 格式约定

为提升脚本的可读性与一致性，建议遵循以下格式规范：

1. **赋值对齐**  
   在多行对象构造中，应尽可能将 `=` 对齐，使字段名与表达式清晰分离。对齐以最长字段名为基准，例如：

   ```gml
   id    = user.id
   name  = user.name
   email = user.contact.email
   ```

2. **缩进规则**  
   内嵌对象和数组必须使用 4 个空格 进行缩进，子结构相对于父字段对齐，例如：

   ```gml
   id      = user.id
   address = {
       province = user.addr.prov
       city     = user.addr.city
       street   = user.addr.detail
   }
   items   = [
       { sku = 'sku_01', quantity = 2 },
       { sku = 'sku_02', quantity = 1 }
   ]
   ```

## 2. 语法说明

GML 的映射能力由一系列表达式构成，支持从简单字段提取到复杂逻辑计算的完整数据转换。  
本章系统阐述其语法规范，涵盖值引用、运算、条件控制、作用域管理及复合结构构造。

### 2.1 基本映射

一切表达式始于字面量或对源数据的引用：

```gml
name     = user.profile.name      # 点语法访问嵌套字段
status   = 'active'               # 字符串字面量（单引号）
score    = 95                     # 数值字面量
isMember = true                   # 布尔字面量
firstTag = tags[0]                # 数组首个元素
lastTag  = tags[#]                # 数组末尾元素（# 表示最后）
channel  = source['x-channel']    # 方括号访问含特殊字符的字段
```

- 字段支持任意深度链式访问（如 `a.b.c`）。
- 数组索引从 `0` 开始，`items[0]` 取首元素，`tags[#]` 取末元素（`#` 为合法索引符号，表示最后一个位置）。
- 字符串必须用单引号；模板字符串必须使用反引号（见 2.2 节）；数值、布尔和 `null` 直接书写。

### 2.2 组合表达式

在基础值之上，GML 支持组合表达式：

```gml
id        = `${order.channel}_${order.id}`    # 字符串模板插值（使用反引号）
total     = price * quantity                  # 数学运算
discount  = amount >= 1000 ? 0.9 : 1.0        # 三元条件
eligible  = level > 1 && points > 500         # 逻辑与比较
gender    = user.gender || 'unknown'          # 值为null时，使用默认值
```

- 支持 `+ - * / %` 四则与取余，括号可控制优先级。
- 比较（`>`, `==`）和逻辑操作（`&&`, `||`, `!`）返回布尔值。
- 字符串模板必须使用反引号（\`...\`），其中 `${}` 内可嵌入任意表达式，模板内使用单引号或反引号作为普通字符使用时需在前面加\转义。
- 支持使用`||`为可能为`null`的值，指定默认值。

### 2.3 CASE 表达式

当条件超过两种情况时，使用 `CASE` 提升可读性：

```gml
priority = CASE
               WHEN status == 'urgent' THEN 1
               WHEN amount > 5000      THEN 2
               WHEN is_vip             THEN 3
               ELSE 4
           END
```

- 按 `WHEN` 顺序求值，返回首个匹配的 `THEN` 值。
- 若无匹配且未提供 `ELSE`，结果为 `null`。

### 2.4 作用域与临时变量

在对象构造中，可通过 `$` 前缀声明临时变量，用于封装可复用的中间逻辑；已定义的字段（包括临时变量）可通过 `this` 引用：

```gml
$baseAmount = order.items.sum('amount')
$coupon     = $baseAmount > 200 ? 20 : 0 
total       = this.$baseAmount - this.$coupon
discounted  = this.total < $baseAmount
```

- 所有以 `$` 开头的变量不会出现在输出中-
- `this` 可引用已定义的字段/变量（包括 `$` 变量），支持逻辑复用并避免重复计算。

### 2.5 用户自定义函数（UDF）

当内置能力不足时，可调用预注册的 UDF 扩展功能：

```gml
timeStr = TIME_FORMAT(createTime, 'yyyyMMdd')
key     = CONCAT(userId, '_', order.id)
hash    = MD5(this.key)
```

- 函数调用形式为 `NAME(arg1, arg2, ...)`，参数可为任意表达式。
- 支持嵌套调用（如 `MD5(CONCAT(...))`）。
- 具体可用函数由运行时环境提供，详见《UDF 使用说明文档》。

### 2.6 构造复合结构

GML 支持内联创建对象与数组，直接生成嵌套结果：

#### 对象

```gml
customer  = {
    ...user                                     # 展开 user 所有字段
    ...address.proj('city, mobile')             # 裁剪地址并展开
    level     = account.tier                    # 覆盖或新增字段
    hasMobile = this.customer.mobile != null    # 引用已定义的字段
}
```

- 使用 `{}` 可内联定义对象，其内部的映射语法与顶层完全一致。
- 通过 `...obj` 可展开源对象的所有属性；后续定义的同名字段，会覆盖展开的值。
- `this` 始终指向当前映射的最外层上下文，嵌套对象中可通过类似 `this.customer.mobile` 层级指向的方式，访问各级已有字段。

#### 数组

```gml
items = [
    { sku = 'sku_01', quantity = 2 },
    { sku = 'sku_02', quantity = 1 }
]
tags  = ['new', 'hot']
```

- 使用 `[]` 创建，元素可以是字面量、表达式或对象。
- 多行格式下，子结构缩进 4 个空格。
## 3. 原型方法

GML 支持在表达式中调用数据字段上的原型方法，主要用于对数组等复合类型进行转换、聚合、结构重组等操作。

- 所有方法均保持数据不可变性（immutability），即原数据不变，转换结果以新对象或新数组形式返回。
- 对 `null`进行调用不会报错，将直接返回`null`。

### 3.1 数组原型方法

数组是 GML 中最常用的数据结构之一。通过链式调用数组方法，可以高效地完成复杂的数据处理任务。

#### 1. 转换

用于生成新结构或筛选/投影数据：

| 方法                 | 说明                                   | 示例                                    |
|--------------------|--------------------------------------|---------------------------------------|
| `map(callback)`    | 对每个元素应用转换函数，返回新数组。                   | `items.map(item => item.sku)`         |
| `filter(callback)` | 保留满足条件的元素，返回过滤后的新数组。                 | `orders.filter(o => o.amount > 1000)` |
| `proj(fields)`     | 仅保留对象中的指定字段（多个字段以英文逗号分隔），常用于数据脱敏或精简。 | `orders.proj('channel,amount,time')`  |

#### 2. 聚合

用于从数组中提取统计信息：

| 方法                              | 说明                           | 示例                     |
|---------------------------------|------------------------------|------------------------|
| `sum([field])`                  | 数值数组求和；对象数组则对指定字段求和（空值视为 0）。 | `orders.sum('amount')` |
| `avg([field])`                  | 计算平均值，结果四舍五入最多保留 6 位小数。      | `items.avg('price')`   |
| `min([field])` / `max([field])` | 返回最小值或最大值。                   | `prices.max()`         |
| `med([field])`                  | 返回中位数；若元素个数为偶数，则取中间两个数的平均值。  | `scores.med()`         |
| `length()`                      | 返回数组长度（元素个数）。                | `tags.length()`        |

#### 3. 结构操作

用于改变数组的嵌套层级或组合方式：

| 方法                  | 说明                                                               | 示例                                           |
|---------------------|------------------------------------------------------------------|----------------------------------------------|
| `flat()`            | 对二维数组进行一层扁平化。                                                    | `[[1,2],[3,4]].flat()` → `[1,2,3,4]`         |
| `flat(field)`       | 先提取对象数组中某字段（通常为数组），再扁平化。等效于 `map(field).flat()`。                 | `orders.flat('items')`                       |
| `chunk(size)`       | 将数组按指定大小切分为多个子数组。                                                | `[1,2,3,4,5].chunk(2)` → `[[1,2],[3,4],[5]]` |
| `expand(field)`     | 将对象数组中某个字段（必须为数组）展开为独立记录，其余字段保持不变。展开后，原字段名被替换为其元素值。适用于“一对多”结构展开。 | `orders.expand('items')` 可将每个订单条目变为独立行。      |
| `concat(array)`     | 将当前数组与传入数组拼接，返回拼接后的新数组。                                          | `a.concat(b)`                                |
| `push(...items)`    | 在末尾追加一个或多个元素，并返回新数组。                                             | `tags.push('hot', 'new')`                    |
| `add(...items)`     | 与push等效。                                                         |
| `addAll(array)`     | 与concat等效。                                                       |
| `unshift(...items)` | 在头部追加一个或多个元素。                                                    | `tags.unshift('top')`                        |
| `slice(start, end)` | 截取子数组（含 `start`，不含 `end`）；索引越界时自动裁剪至有效范围，若无有效区间则返回空数组。           | `nums.slice(0, 2)`                           |

#### 4. 分组与折叠

用于按业务维度组织数据：

| 方法                                        | 说明                                                                                                | 示例                                            |
|-------------------------------------------|---------------------------------------------------------------------------------------------------|-----------------------------------------------|
| `group(field)`                            | 按指定字段值分组，返回一个对象：键为字段值（字符串形式），值为对应元素组成的数组。                                                         | `orders.group('channel')`                     |
| `collap(field, sortExpression, maxCount)` | 先按 `field` 分组，组内按 `sortExpression`（如 `'time DESC'`）排序，每组最多保留 `maxCount` 条记录。常用于“取每个用户最近 N 笔订单”场景。 | `orders.collap('customerId', 'time DESC', 2)` |

#### 5. 查询与判断

用于检测数组内容是否满足特定条件：

| 方法                                        | 说明                      | 示例                                      |
|-------------------------------------------|-------------------------|-----------------------------------------|
| `some(callback)`                          | 判断是否存在至少一个元素满足条件，返回布尔值。 | `items.some(i => i.category == 'food')` |
| `every(callback)`                         | 判断是否所有元素都满足条件。          | `orders.every(o => o.amount > 0)`       |
| `includes(searchElement, [fromIndex])`    | 判断数组是否包含某元素（可选起始搜索位置）。  | `tags.includes('vip')`                  |
| `indexOf(searchElement, [fromIndex])`     | 返回元素首次出现的索引，未找到返回 `-1`。 | `tags.indexOf('b')`                     |
| `lastIndexOf(searchElement, [fromIndex])` | 从后向前查找，返回最后一次出现的索引。     | `tags.lastIndexOf('b')`                 |

#### 6. 排序

用于控制元素顺序：

| 方法           | 说明                                                 | 示例                                     |
|--------------|----------------------------------------------------|----------------------------------------|
| `sort()`     | 对基本类型数组按升序排序（数值或字典序）。                              | `numbers.sort()`                       |
| `sort(sort)` | 对对象数组按字段表达式排序，格式为 `'field1 ASC, field2 DESC'`。     | `orders.sort('amount DESC, time ASC')` |
| `reverse()`  | 反转数组元素顺序。常与 `sort()` 配合实现降序：`arr.sort().reverse()` | `arr.sort().reverse()`                 |

#### 7. 去重与拼接

用于格式化输出或消除冗余：

| 方法                | 说明                       | 示例                                 |
|-------------------|--------------------------|------------------------------------|
| `distinct()`      | 去除重复元素，生成唯一值数组。          | `[1,2,2,3].distinct()` → `[1,2,3]` |
| `join(separator)` | 将基本类型数组元素连接为字符串，使用指定分隔符。 | `['a','b'].join(',')` → `'a,b'`    |

> **提示**：所有数组方法均支持链式调用，且不修改原数组。例如：
>
> ```gml
> orders
>   .filter(o => o.amount > 500)
>   .map(o => o.channel)
>   .distinct()
> ```

### 3.2 对象原型方法

GML 支持在对象上调用以下原型方法：

| 方法             | 说明                                   | 示例                                                                       |
|----------------|--------------------------------------|--------------------------------------------------------------------------|
| `proj(fields)` | 提取指定字段（以英文逗号分隔的字符串），返回新对象；不存在的字段被忽略。 | `user.proj('name, email')` → `{ name: 'Alice', email: 'a@example.com' }` |

> 提示：`proj` 可以与对象展开语法组合使用，如：...user.proj('name, email')

### 3.3 时间原型方法

GML 支持在日期类型上，调用以下原型方法：

| 方法                 | 说明                                                   | 示例                               |
|--------------------|------------------------------------------------------|----------------------------------|
| `offset(duration)` | 对时间进行调整，`duration` 为时间偏移表达式，格式为：正负数值 + 时间单位(yMdhms)。 | `now.offset('30s')` → 当前时间加 30 秒 |

### 3.4 字符串原型方法

GML 支持在字符串类型上调用以下原型方法：

| 方法              | 说明                   | 示例                                  |
|-----------------|----------------------|-------------------------------------|
| `toLowerCase()` | 返回全小写字符串。            | `'Hello'.toLowerCase()` → `'hello'` |
| `toUpperCase()` | 返回全大写字符串。            | `'Hello'.toUpperCase()` → `'HELLO'` |
| `length()`      | 返回字符串的 Unicode 字符长度。 | `'你好!'.length()` → `3`              |

